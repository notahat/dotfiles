function _configure_homebrew {
  if [[ -f /opt/homebrew/bin/brew ]] {
    export HOMEBREW_NO_ENV_HINTS=true

    eval "$(/opt/homebrew/bin/brew shellenv)"

    fpath=($HOMEBREW_PREFIX/share/zsh/site-functions $fpath)
    fpath=($HOMEBREW_PREFIX/share/zsh-completions $fpath)
  }
}

function _configure_completion {
  autoload -U compinit && compinit

  if [[ -f $HOMEBREW_PREFIX/bin/fzf ]] {
    source <(fzf --zsh)

    if [[ -d ~/.config/zsh/plugins/fzf-tab ]] {
      # Use fzf as a completion menu for everything. This needs to load after
      # compinit, but before other plugins.
      source ~/.config/zsh/plugins/fzf-tab/fzf-tab.plugin.zsh
    }
  }
}

function _load_zsh_syntax_highlighting_plugin {
  if [[ -d $HOMEBREW_PREFIX/share/zsh-syntax-highlighting ]] {
    source $HOMEBREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
  }
}

function _configure_mise {
  if [[ -f $HOMEBREW_PREFIX/bin/mise ]] {
    eval "$(mise activate zsh --shims)"
  }
}

function _configure_prompt {
  newline=$'\n'
  path_prompt_info='%F{blue}%~%f'

  autoload -U vcs_info
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' unstagedstr '*'
  zstyle ':vsc_info:*' check-for-staged-changes true
  zstyle ':vcs_info:*' stagedstr '+'
  zstyle ':vcs_info:*' formats '%F{green}[%b%u%c]%f'
  function precmd { vcs_info }

  setopt prompt_subst
  PROMPT='${newline}${path_prompt_info} ${vcs_info_msg_0_} %# '
}

function _configure_environment {
  export BAT_STYLE=numbers,changes
  export BAT_THEME=base16
  export CLICOLOR=1  # Make ls colour its output.
  export FZF_DEFAULT_COMMAND='fd --type f'
  export LESS=-R     # Make less support ANSI colour sequences.

  if [[ -f $HOMEBREW_PREFIX/bin/nvim ]] {
    export EDITOR=$HOMEBREW_PREFIX/bin/nvim
  }

  case $(hostname -s) {
    Knuth)
      export DOTFILES_ENV=home;;
    *)
      export DOTFILES_ENV=work;;
  }

  export UP_HOME_PATH=~/src/up
}

function _configure_aliases {
  alias be='bundle exec'
  alias br='./bin/rails'

  alias cdr='cd $(git root)'

  alias ga='git add'
  alias gc='git commit'
  alias gco='git checkout'
  alias gd='git diff'
  alias gf='git fetch'
  alias gff='git merge --ff-only'
  alias gl='git log'
  alias gm='git merge --no-ff'
  alias gp='git push'
  alias gpr='git push -u && gh pr create --web'
  alias gb='gh pr view --web'
  alias gs='git status'

  alias zed='/usr/local/bin/zed'

  alias mvim='NVIM_APPNAME=nvim-up-starter nvim'

  function autonode { echo $1 | entr -c node $1 }
  function autoruby { echo $1 | entr -c ruby $1 }
  function autorspec { echo $1 | entr -c ./up rspec $1 }

  alias sup='OVERMIND_IGNORED_PROCESSES=api,api_docs,admin_storybook,remix_packager,remix_storybook,remix_appium,remix_relay ./up'
}

# Start with a clean path.
export PATH=/usr/bin:/bin:/usr/sbin:/sbin

# Configure homebrew and completion early, so other things can build on them.
_configure_homebrew
_configure_completion

# Other plugins can happen after completion is set up.
_load_zsh_syntax_highlighting_plugin

# Use vim keybindings.
bindkey -v
KEYTIMEOUT=1

_configure_mise
_configure_prompt
_configure_environment
_configure_aliases
